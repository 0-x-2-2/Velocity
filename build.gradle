buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'com.github.jengelman.gradle.plugins:shadow:5.0.0'
    }
}

plugins {
    id 'java'
    id 'org.checkerframework' version '0.5.4' apply false
    id 'checkstyle'
}

allprojects {
    repositories {
        mavenLocal()
        mavenCentral()

        // for kyoripowered dependencies
        maven {
            url 'https://oss.sonatype.org/content/groups/public/'
        }

        // Brigadier
        maven {
            url "https://libraries.minecraft.net"
        }

        // Configurate
        maven {
            name = 'sponge'
            url = 'https://repo.spongepowered.org/maven'
        }
    }

    ext {
        // dependency versions
        textVersion = '3.0.3'
        junitVersion = '5.3.0-M1'
        slf4jVersion = '1.7.25'
        log4jVersion = '2.11.2'
        nettyVersion = '4.1.50.Final'
        guavaVersion = '25.1-jre'
        checkerFrameworkVersion = '3.4.1'
        configurateVersion = '3.6'

        getCurrentShortRevision = {
            new ByteArrayOutputStream().withStream { os ->
                exec {
                    executable = "git"
                    args = ["rev-parse", "HEAD"]
                    standardOutput = os
                }
                return os.toString().trim().substring(0, 8)
            }
        }
    }
}

subprojects { subproject ->
        group 'com.velocitypowered'
        version '1.1.0-SNAPSHOT'

        sourceCompatibility = 1.8
        targetCompatibility = 1.8

        apply plugin: 'java'
        apply plugin: 'checkstyle'
        apply plugin: 'org.checkerframework'

        checkstyle {
            toolVersion '8.14'
            configFile new File(project.rootDir, ['config', 'checkstyle', 'checkstyle.xml'].join(File.separator))

            // The build should immediately fail if we have errors.
            maxErrors = 0
            maxWarnings = 0
        }

        apply plugin: 'org.checkerframework'

        checkerFramework {
            if (subproject.name == 'velocity-native') {
                checkers = ['org.checkerframework.checker.nullness.NullnessChecker',
                            'org.checkerframework.checker.optional.OptionalChecker']
            } else {
                checkers = ['org.checkerframework.checker.optional.OptionalChecker']
            }
        }

        dependencies {
            checkerFramework 'org.checkerframework:checker:3.4.1'
        }
}
